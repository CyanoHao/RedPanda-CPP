_pkgname=RedPanda-CPP
pkgname=${_pkgname,,}-git
pkgver=__VERSION__
pkgrel=1
pkgdesc='A fast, lightweight, open source, and cross platform C++ IDE (development version)'
arch=('i686' 'pentium4' 'x86_64' 'arm' 'armv6h' 'armv7h' 'aarch64' 'riscv64')
url="https://github.com/royqh1979/$_pkgname"
license=('GPL3')
depends=(qt6-base qt6-svg gcc gdb astyle)
makedepends=(qt6-tools cmake imagemagick librsvg)
optdepends=(
    'clang: C/C++ compiler (alternative)'
)
conflicts=("${_pkgname,,}")
provides=("${_pkgname,,}")
source=(
    "$_pkgname.tar.gz"
    'compiler_hint.lua'
)
sha256sums=(
    'SKIP'
    'SKIP'
)

build() {
    mkdir redpanda-build
    cd redpanda-build
    cmake -S "$srcdir/$_pkgname" -B . \
        -G "Unix Makefiles" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBEXECDIR=lib
    make
}

package() {
    local _libexecdir="$pkgdir/usr/lib/RedPandaCPP"
    install -Dm644 "compiler_hint.lua" "$_libexecdir/compiler_hint.lua"

    cd redpanda-build
    make DESTDIR="$pkgdir" install

    for size in 16 22 24 32 36 48 64 72 96 128 192 256 512; do
        mkdir -p "$pkgdir/usr/share/icons/hicolor/${size}x${size}/apps"
        magick convert \
            -background none \
            "$pkgdir/usr/share/icons/hicolor/scalable/apps/redpandaide.svg" \
            -resize ${size}x${size} \
            "$pkgdir/usr/share/icons/hicolor/${size}x${size}/apps/redpandaide.png"
    done
}
